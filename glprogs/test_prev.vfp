!!ARBvp1.0 OPTION ARB_position_invariant ;

# DarkMod Enhanced Interaction
# Version 280809
# Author : rebb ( rebb [at] trisoup [dot] net )

# Different changes, including rebb's Ambient-Light Fix.
# Improves the looks of Doom3 Ambient Lights, removing the "Coal Mine"-Look.
# The Projection-Texture ( not lightFalloffImage ) of the ambient-light's material must have an alpha-channel
# with a Value of 1 ( on the 0-255 scale ), 0 doesn't work because Doom3 automatically "optimizes" black alpha-channels away completely.
# If this is not the case, ambient-lights will not work correctly when this VFP is enabled !

# Many thanks for helping in testing go to :
#   New Horizon and the DarkMod Team, Neuling, chumbucket91, The Happy Friar
#   the TTLG boards and Doom3World

PARAM defaultTexCoord = { 0, 0.5, 0, 1 };
TEMP R0, R1, R2;
SUB R0, program.env[4], vertex.position;
DP3 result.texcoord[0].x, vertex.attrib[9], R0;
DP3 result.texcoord[0].y, vertex.attrib[10], R0;
DP3 result.texcoord[0].z, vertex.attrib[11], R0;
MOV result.texcoord[1], defaultTexCoord;
DP4 result.texcoord[1].x, vertex.attrib[8], program.env[10];
DP4 result.texcoord[1].y, vertex.attrib[8], program.env[11];
MOV result.texcoord[2], defaultTexCoord;
DP4 result.texcoord[2].x, vertex.position, program.env[9];
DP4 result.texcoord[3].x, vertex.position, program.env[6];
DP4 result.texcoord[3].y, vertex.position, program.env[7];
DP4 result.texcoord[3].w, vertex.position, program.env[8];
MOV result.texcoord[4], defaultTexCoord;
DP4 result.texcoord[4].x, vertex.attrib[8], program.env[12];
DP4 result.texcoord[4].y, vertex.attrib[8], program.env[13];
MOV result.texcoord[5], defaultTexCoord;
DP4 result.texcoord[5].x, vertex.attrib[8], program.env[14];
DP4 result.texcoord[5].y, vertex.attrib[8], program.env[15];
DP3 R1.x, R0, R0;
RSQ R1.x, R1.x;
MUL R0, R0, R1.x;
SUB R1, program.env[5], vertex.position;
DP3 R2.x, R1, R1;
RSQ R2.x, R2.x;
MUL R1, R1, R2.x;
ADD R0, R0, R1;
DP3 result.texcoord[6].x, vertex.attrib[9], R0;
DP3 result.texcoord[6].y, vertex.attrib[10], R0;
DP3 result.texcoord[6].z, vertex.attrib[11], R0;
ADD R0, R1, vertex.attrib[11];
MUL R1, R0, 0.5;
DP3 result.texcoord[7].x, vertex.attrib[9], R1;
DP3 result.texcoord[7].y, vertex.attrib[10], R1;
DP3 result.texcoord[7].z, vertex.attrib[11], R1;
MAD result.color, vertex.color, program.env[16], program.env[17];
END

!!ARBfp1.0
OPTION ARB_precision_hint_fastest;
PARAM x0 = { 8,1,1,1 };
PARAM x1 = { -0.57735, -0.57735, 0.57735 };
TEMP light, color, R0, R1, R2, R3, R4, localNormal, specular;
OUTPUT outCol = result.color;
TEX localNormal, fragment.texcoord[1], texture[1], 2D;
MOV localNormal.x, localNormal.a;
MAD localNormal.xyz, localNormal, 2.0, -1.0;
DP3 light.w, fragment.texcoord[0], fragment.texcoord[0];
DP3 specular.w, fragment.texcoord[6], fragment.texcoord[6];
DP3 localNormal.w, localNormal, localNormal;
MUL R0.w, light.w, localNormal.w;
RSQ R0.w, R0.w;
DP3 light.w, fragment.texcoord[0], localNormal;
MUL light.w, light.w, R0.w;
TXP R1, fragment.texcoord[3], texture[3], 2D;
SUB R4.w, R1.w, 0.01;
TXP R2, fragment.texcoord[2], texture[2], 2D;
MUL R0.xyz, R1, R2;
MUL light.xyz, light.w, R0;
TEX R1, fragment.texcoord[4], texture[4], 2D;
MUL color.xyz, R1, program.env[0];
MUL R0.w, specular.w, localNormal.w;
RSQ R0.w, R0.w;
DP3 specular.w, fragment.texcoord[6], localNormal;
MUL specular.w, specular.w, R0.w;
MAD_SAT R1.w, specular.w, 4.0, -3.0;
MUL R1.w, R1.w, R1.w;
MUL R1.xyz, R1.w, program.env[1];
DP3 R4.x, x1, localNormal;
MAD R4.x, R4.x, 0.6, 0.25;
DP3_SAT R4.y, localNormal, fragment.texcoord[7];
MUL R4.x, R4.x, R4.y;
MAD R4.x, R4.x, 0.8, 0.4;
MUL R4.x, R4.x, localNormal.z;
MUL R0.xyz, R0, R4.x;
CMP light.xyz, R4.w, R0, light;
TEX R2, fragment.texcoord[5], texture[5], 2D;
ADD R2.xyz, R2, R2;
MAD color.xyz, R1, R2, color;
MUL_SAT color.xyz, light, color;
MUL outCol.xyz, color, fragment.color;
END